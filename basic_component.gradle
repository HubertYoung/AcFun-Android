apply from: rootProject.file('cc-settings.gradle')
//apply from: 'https://raw.githubusercontent.com/luckybilly/CC/master/cc-settings.gradle'
//先加载local.properties文件
Properties localProperties = new Properties()
try {
    def localFile = project.rootProject.file('local.properties')
    if (localFile != null && localFile.exists()) {
        localProperties.load(localFile.newDataInputStream())
    }
} catch (Exception ignored) {
    println("local.properties not found")
}

//读取build.gradle中的设置
// 2018-04-06修改：
//  为了更利于理解，将ext.runAsApp 改名为 ext.mainApp
//  ext.mainApp的将仅代表是否作为主app，为true时以application方式编译，为false或未配置时以local.properties中的配置为准
//  兼容以前的runAsApp设置，ext.runAsApp的功能保持不变，runAsApp优先级高于local.properties
def runAsApp = ext.has('runAsApp')
if (runAsApp) {
    runAsApp = ext.runAsApp
} else if(ext.has('mainApp') && ext.mainApp) { //ext.mainApp为true时，代表以app方式运行
    runAsApp = true
} else {
    //build.gradle中没有配置runAsApp，且ext.mainApp=false或未配置
    //再从local.properties中读取配置，例如： demo_component_a=true
    //注：如果采用local.properties读取配置，每次修改需要重新同步(Sync Project)一下
    runAsApp = 'true' == localProperties.getProperty(project.name)
    //设置到ext中，供module的build.gradle使用（例如用于设置sourceSets配置）
    ext.runAsApp = runAsApp
}

//模块名称
def componentName = project.name.replaceAll("component_", "")
println(project.name)
android {
    compileSdkVersion var.compileSdkVersion
    defaultConfig {
        minSdkVersion var.minSdkVersion
        targetSdkVersion var.targetSdkVersion
        versionCode var.versionCode
        versionName var.versionName
        if (runAsApp) {
            applicationId var.applicationId + componentName
        }
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
        resourcePrefix componentName + "_"
        resValue "string", componentName + "_module_name", project.name
        signingConfigs {
            config {
                keyAlias localProperties['keyAlias']
                keyPassword localProperties['keyPassword']
                storeFile file(localProperties['storeFile'])
                storePassword localProperties['storePassword']
//            v2SigningEnabled false
            }
        }
        vectorDrawables.useSupportLibrary = true
    }


    buildTypes {
        release {
            signingConfig signingConfigs.config
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            signingConfig signingConfigs.config
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }
//    androidExtensions {
//        experimental = true
//    }
}


dependencies {
    api fileTree(include: ['*.jar'], dir: 'libs')
    api "androidx.legacy:legacy-support-v4:$var.androidxLegacySupportV4"
    api "androidx.appcompat:appcompat:$var.androidxAppcompat"
    api "com.google.android.material:material:$var.androidxMaterial"
    api "androidx.cardview:cardview:$var.androidxCardview"
    api "androidx.gridlayout:gridlayout:$var.androidxGridlayout"
    api "androidx.preference:preference:$var.androidxPreference"
    api "androidx.constraintlayout:constraintlayout:$var.androidxConstraintlayout"

    api "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    //view注解
//    implementation 'com.jakewharton:butterknife:8.4.0'
//    kapt 'com.jakewharton:butterknife-compiler:8.4.0'

    //retrofit和rxjava
    api "com.squareup.retrofit2:retrofit:$var.retrofit"
    implementation "com.squareup.retrofit2:converter-gson:$var.retrofit"
    implementation "com.squareup.retrofit2:converter-scalars:$var.retrofit"
    api "com.squareup.retrofit2:adapter-rxjava2:$var.retrofit"
    implementation "com.squareup.okhttp3:logging-interceptor:$var.okhttplog"
    api "io.reactivex.rxjava2:rxandroid:$var.rxandroid"
    // explicitly depend on RxJava's latest version for bug fixes and new features.
    api "io.reactivex.rxjava2:rxjava:$var.rxjava"
//    api 'com.trello.rxlifecycle2:rxlifecycle-components:2.2.2'
    api "com.jakewharton.rxbinding2:rxbinding:$var.rxbinding"
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.1.0-alpha4'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.0-alpha4'
}